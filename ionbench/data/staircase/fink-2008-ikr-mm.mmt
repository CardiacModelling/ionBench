[[model]]
author: Matt Owen
name: fink_noble_giles_model_2008
# Initial values
iKr_Markov.Cr1                = 1
iKr_Markov.Cr2                = 0
iKr_Markov.Cr3                = 0
iKr_Markov.Or4                = 0
iKr_Markov.Ir5                = 0

[Environment]
F =  9.64853414999999950e4 [C/mol]
    in [C/mol]
K_i = 141.0167 [mM]
    in [mM]
K_o = 5.4 [mM]
    in [mM]
R = 8314.472 [mJ/mol/K]
    in [mJ/mol/K]
T = 310 [K]
    in [K]
E_K = R * T / F * log(K_o / K_i)
    in [mV]

[engine]
time = 0 [ms] bind time
    in [ms]
pace = -80 [mV] bind pace
    in [mV]

[membrane]
V = engine.pace
    desc: membrane potential
    in [mV]

[IKr]
i_Kr = iKr_Markov.p15 * (Environment.T / 35 [K] - 55 / 7) * sqrt(Environment.K_o / 5.4 [mM]) * iKr_Markov.Or4 * (membrane.V - Environment.E_K)
    in [A/F]

[iKr_Markov]
use membrane.V
dot(Cr1) = beta_xr1 * Cr2 - alpha_xr1 * Cr1
    in [1]
dot(Cr2) = alpha_xr1 * Cr1 + beta_xr2 * Cr3 - (alpha_xr2 + beta_xr1) * Cr2
    in [1]
dot(Cr3) = alpha_xr2 * Cr2 + beta_xr3 * Or4 - (alpha_xr3 + beta_xr2) * Cr3
    in [1]
dot(Ir5) = alpha_xr4 * Or4 - beta_xr4 * Ir5
    in [1]
dot(Or4) = alpha_xr3 * Cr3 + beta_xr4 * Ir5 - (alpha_xr4 + beta_xr3) * Or4
    in [1]
alpha_xr1 = 1 [mS/uF] * p1 * exp(p2 * V)
    in [mS/uF]
alpha_xr2 = 1 [mS/uF] * p3
    in [mS/uF]
alpha_xr3 = 1 [mS/uF] * p4 * exp(p5 * V)
    in [mS/uF]
alpha_xr4 = 1 [mS/uF] * p6 * exp(p7 * V) * (5.4 [mM] / Environment.K_o)^0.4
    in [mS/uF]
beta_xr1 = 1 [mS/uF] * p8 * exp(-p9 * V)
    in [mS/uF]
beta_xr2 = 1 [mS/uF] * p10
    in [mS/uF]
beta_xr3 = 1 [mS/uF] * p11 * exp(-p12 * V)
    in [mS/uF]
beta_xr4 = 1 [mS/uF] * p13 * exp(-p14 * V)
    in [mS/uF]

p1 = 0.20618 [1]
    in [1]
p2 = 0.0112 [mV^-1]
    in [mV^-1]
p3 = 0.04209 [1]
    in [1]
p4 = 0.02202 [1]
    in [1]
p5 = 0.0365 [mV^-1]
    in [mV^-1]
p6 = 0.41811 [1]
    in [1]
p7 = 0.0223 [mV^-1]
    in [mV^-1]
p8 = 0.13279 [1]
    in [1]
p9 = 0.0603 [mV^-1]
    in [mV^-1]
p10 = 0.08094 [1]
    in [1]
p11 = 0.0002262 [1]
    in [1]
p12 = 0.0399 [mV^-1]
    in [mV^-1]
p13 = 0.04150 [1]
    in [1]
p14 = 0.0312 [mV^-1]
    in [mV^-1]
p15 = 0.024 [mS/uF]
    in [mS/uF]

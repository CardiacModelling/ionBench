[[model]]
name: Moreno 2016 INa
author: Matt Owen
# Initial values
ina.ic3 = 0
ina.ic2 = 0
ina.if = 0
ina.c3 = 1
ina.c2 = 0
ina.c1 = 0
ina.o = 0
ina.is = 0

#
# Simulation engine variables
#
[engine]
time = 0 [ms] bind time
    in [ms]
pace = -80 [mV] bind pace
    in [mV]

#
# Membrane potential
#
[membrane]
V = engine.pace
    desc: membrane potential
    in [mV]
ENa = R*T/F*log(Nao/Nai)
    in [mV]
Nao = 138 [mmol] #from Global_Variables.h in Moreno 2016 code
    in [mmol]
Nai = 10 [mmol] #from Global_Variables.h in Moreno 2016 code
    in [mmol]
F =  9.6485e4 [C/mol]
    in [C/mol]
R = 8314.472 [mJ/mol/K]
    in [mJ/mol/K]
T = 295 [K] #22C
    in [K]

[ina]
use membrane.V
INa = gNa * o * (V - membrane.ENa)
    in [A/F]
dot(ic3) = -ic3*(alpha11+alpha3) + beta3*c3 + beta11*ic2
dot(ic2) = -ic2*(beta11+alpha3+alpha12) + alpha11*ic3 + beta3*c2 + beta12*if
dot(if) = -if*(beta12+alpha3+beta2) + alpha12*ic2 + beta3*c1 + alpha2*o
dot(c3) = -c3*(beta3+alpha11) + beta11*c2 + alpha3*ic3
dot(c2) = -c2*(beta11+beta3+alpha12) + alpha11*c3 + alpha3*ic2 + beta12*c1
dot(c1) = -c1*(beta12+beta3+alpha13) + alpha12*c2 + alpha3*if + beta13*o
dot(o) = -o*(beta13+alpha2+alphax) + alpha13*c1 + beta2*if + betax*is
dot(is) = -betax*is + alphax*o
total = ic3+ic2+if+c3+c2+c1+o+is
alpha11 = 1/(a11v1*exp(-V/a11v2))
    in [1/ms]
alpha12 = a12*alpha11
    in [1/ms]
alpha13 = a13*alpha11
    in [1/ms]
beta11 = 1/(b11v1*exp(V/b11v2))
    in [1/ms]
beta12 = b12*beta11
    in [1/ms]
beta13 = b13*beta11
    in [1/ms]
alpha3 = a3v1*exp(-V/a3v2)
    in [1/ms]
beta3 = b3v1*exp(V/b3v2)
    in [1/ms]
alpha2 = a2v1*exp(V/a2v2)
    in [1/ms]
beta2 = alpha13*alpha2*alpha3/(beta13*beta3)
    in [1/ms]
alphax = ax*alpha2
    in [1/ms]
betax = bx*alpha3
    in [1/ms]

p1 = 7.6178e-3 [ms]
    in [ms]
a11v1 = p1
    in [ms]
p2 = 3.2764e1 [mV]
    in [mV]
a11v2 = p2
    in [mV]
p3 = 5.8871e-1 [1]
    in [1]
a12 = p3
    in [1]
p4 = 1.5422e-1 [1]
    in [1]
a13 = p4
    in [1]
p5 = 2.5898 [ms]
    in [ms]
b11v1 = p5
    in [ms]
p6 = 8.5072 [mV]
    in [mV]
b11v2 = p6
    in [mV]
p7 = 1.3760e-3 [1]
    in [1]
b12 = p7
    in [1]
p8 = 2.888 [1]
    in [1]
b13 = p8
    in [1]
p9 = 3.2459e-5 [1/ms]
    in [1/ms]
a3v1 = p9
    in [1/ms]
p10 = 9.5951 [mV]
    in [mV]
a3v2 = p10
    in [mV]
p11 = 1.3771 [1/ms]
    in [1/ms]
b3v1 = p11
    in [1/ms]
p12 = 2.1126e1 [mV]
    in [mV]
b3v2 = p12
    in [mV]
p13 = 1.1086e1 [1/ms]
    in [1/ms]
a2v1 = p13
    in [1/ms]
p14 = 4.3725e1 [mV]
    in [mV]
a2v2 = p14
    in [mV]
p15 = 4.1476e-2 [1]
    in [1]
ax = p15
    in [1]
p16 = 2.0802e-2 [1]
    in [1]
bx = p16
    in [1]
gNa = 1 [nS/pF]
    in [nS/pF]
